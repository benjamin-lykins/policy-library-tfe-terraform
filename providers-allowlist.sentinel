# This policy blocks Terraform Apply when a provider not listed in the `allowed_providers` parameter is detected in the configuration.
# NOTE: this policy should not be considered as full mitigation against threats from potentially malicious providers, due to the fact that provider code is executed during the plan phase. This policy is intended to provide a basic level of protection against accidental use of disallowed providers.

import "tfconfig/v2" as tfconfig

param allowed_providers default ["registry.terraform.io/hashicorp/aws"]

violating_providers = filter tfconfig.providers as _, p {
	p.full_name not in allowed_providers
}

if length(violating_providers) > 0 {
	print("These providers are not allowed:")
	for violating_providers as _, p {
		print(" - " + _ + " (" + p.full_name + ")")
	}
}

main = rule {
	length(violating_providers) == 0
}
